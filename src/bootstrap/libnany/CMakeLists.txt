project(libnany)


get_filename_component(libnany_config_folder "${CMAKE_CURRENT_BINARY_DIR}" REALPATH)
file(MAKE_DIRECTORY "${libnany_config_folder}")

if (NOT "${nany_version_string}" STREQUAL "${__LIBNANY_CONFIG_HEADER_CACHE}"
	OR NOT EXISTS "${libnany_config_folder}/libnany-version.h")
	nmessage("generating 'libnany-version.h' in ${libnany_config_folder}/libnany-version.h")
	configure_file("version.h.cmake" "${libnany_config_folder}/libnany-version.h" @ONLY)
	set(__LIBNANY_CONFIG_HEADER_CACHE "${nany_version_string}" CACHE STRING "" FORCE)
else()
	nmessage("ignoring libnany-version.h ${libnany_config_folder}/libnany-version.h (cache)")
endif()

if (NOT EXISTS "${libnany_config_folder}/libnany-config.h")
	nmessage("generating 'libnany-config.h' in ${libnany_config_folder}/libnany-config.h")
	configure_file("config.h.cmake" "${libnany_config_folder}/libnany-config.h" @ONLY)
else()
	nmessage("ignoring libnany-config.h ${libnany_config_folder}/libnany-config.h (already exists)")
endif()

if (NOT EXISTS "${libnany_config_folder}/libnany-traces.h")
	nmessage("generating 'libnany-traces.h' in ${libnany_config_folder}/libnany-traces.h")
	configure_file("traces.h.cmake" "${libnany_config_folder}/libnany-traces.h" @ONLY)
else()
	nmessage("ignoring libnany-traces.h ${libnany_config_folder}/libnany-traces.h (already exists)")
endif()


macro(addsrc)
	set(options)
	set(oneValueArgs NAME GROUP)
	set(multiValueArgs FILES)
	cmake_parse_arguments(O "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

	set("${O_NAME}" ${O_FILES})
	source_group("${O_GROUP}" FILES ${O_FILES})
endmacro()



#
# generate nsl:std.core
#
get_filename_component(nsl_stdcore_root "${CMAKE_CURRENT_LIST_DIR}/../../nsl/std.core/" REALPATH)
get_filename_component(nsl_root "${CMAKE_CURRENT_LIST_DIR}/../../nsl/" REALPATH)
file(GLOB_RECURSE ny_core_files "${nsl_stdcore_root}/*.ny")

set(ny_core_files
	# std.core
	"${nsl_root}/std.core/bool.ny"
	"${nsl_root}/std.core/string.ny"
	"${nsl_root}/std.core/f64.ny"
	"${nsl_root}/std.core/f32.ny"
	"${nsl_root}/std.core/i64.ny"
	"${nsl_root}/std.core/u64.ny"
	"${nsl_root}/std.core/i32.ny"
	"${nsl_root}/std.core/u32.ny"
	"${nsl_root}/std.core/i16.ny"
	"${nsl_root}/std.core/u16.ny"
	"${nsl_root}/std.core/u8.ny"
	"${nsl_root}/std.core/i8.ny"
	# C types
	"${nsl_root}/std.c/ctypes.ny"

	# std.math
	"${nsl_root}/std.math/math.ny"

	# std.os
	"${nsl_root}/std.os/process.ny"

	# std.io
	"${nsl_root}/std.io/native/native.ny"
	"${nsl_root}/std.io/native/file.ny"
	# std.env
	"${nsl_root}/std.env/env.ny"

	# std.memory
	#"${nsl_root}/std.memory/allocator.ny"
	)

file(MAKE_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}/details/nsl/generated")
set(__nany_stdcore)
set(nysrc_nsl_stdcore)
foreach (filename ${ny_core_files})
	string(REPLACE "/std." "/std" __funcname "${filename}")
	string(REPLACE "${nsl_root}/" "nany_nsl_" __funcname "${__funcname}")
	string(REPLACE ".ny" "" __funcname "${__funcname}")
	string(REPLACE "/" "_" __funcname "${__funcname}")
	string(REPLACE "\\" "_" __funcname "${__funcname}")

	set(target_filename "generated/${__funcname}.c")
	string(REPLACE "_" "-" target_filename "${target_filename}")
	list(APPEND __nany_stdcore "${__funcname}")
	list(APPEND nysrc_nsl_stdcore "${CMAKE_CURRENT_LIST_DIR}/details/nsl/${target_filename}")

	add_custom_command(
		OUTPUT  "${CMAKE_CURRENT_LIST_DIR}/details/nsl/${target_filename}"
		COMMAND "$<TARGET_FILE:nyt-nanyfile-to-cstring>"
			"${__funcname}"
			"${filename}"
			"${CMAKE_CURRENT_LIST_DIR}/details/nsl/${target_filename}"
		DEPENDS
			nyt-nanyfile-to-cstring
			"${filename}"
		VERBATIM)
endforeach()


set(__nsl_stdcore_cpp "#include \"import-stdcore.h\"\n")
set(__nsl_stdcore_cpp "#include \"details/context/project.h\"\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}#include <yuni/yuni.h>\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}#include <yuni/core/string.h>\n\n\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}// !! file generated by cmake\n\n\n")
foreach (element ${__nany_stdcore})
	set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}extern \"C\" const char* ${element}(size_t*);\n")
endforeach()
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\n\n\nnamespace Nany\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}{\n\n\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\tvoid importNSLCore(Project& project)\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t{\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\tsize_t len;\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\tAnyString content;\n\n")

foreach (__funcname ${__nany_stdcore})
	set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\t// ${__funcname}\n")
	set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\t{\n")
	set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\t\tauto* cstr = ${__funcname}(&len);\n")
	set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\t\tcontent.adapt(cstr, static_cast<uint32_t>(len));\n")
	string(REPLACE "_" ":" __sourceid "${__funcname}")
	string(REPLACE "nany:" "" __sourceid "${__sourceid}")
	string(REPLACE "stdcore" "std.core" __sourceid "${__sourceid}")
	string(REPLACE "nsl:" "{nsl} " __sourceid "${__sourceid}")
	if (NOT "${CMAKE_BUILD_TYPE}" STREQUAL "Release" AND NOT "${CMAKE_BUILD_TYPE}" STREQUAL "release")
		set(__sourceid "${__sourceid} > ${nsl_stdcore_root}/${__funcname}.ny")
		string(REPLACE "nany_nsl_stdcore_" "" __sourceid "${__sourceid}")
	endif()
	set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\t\tproject.targets.nsl->addSource(\"${__sourceid}\", content);\n")
	set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t\t}\n")
endforeach()

set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\t}\n")
set(__nsl_stdcore_cpp "${__nsl_stdcore_cpp}\n\n} // namespace Nany\n")
file(WRITE "${CMAKE_CURRENT_LIST_DIR}/details/nsl/generated/all.cpp" "${__nsl_stdcore_cpp}")
list(APPEND nysrc_nsl_stdcore "${CMAKE_CURRENT_LIST_DIR}/details/nsl/generated/all.cpp")










addsrc(
	NAME  nysrc_grammar
	GROUP "grammar"
	FILES ${NANY_YGR})
addsrc(
	NAME  nysrc_details_grammar
	GROUP "details/grammar"
	FILES ${NANY_GRAMMAR_H} ${NANY_GRAMMAR_HXX} ${NANY_GRAMMAR_CPP})




addsrc(
	NAME  nysrc_details_pass_build_ast_normalize
	GROUP "details/pass/build-ast-normalize"
	FILES
	"details/pass/build-ast-normalize/ast-normalize.cpp")


# pass - build - ast from source
addsrc(
	NAME  nysrc_details_pass_build_ast_from_source
	GROUP "details/pass/build-ast-from=source"
	FILES
	"details/pass/build-ast-from-source/ast-from-source.cpp")


addsrc(
	NAME  nysrc_details_pass_build_ast_to_ir
	GROUP "details/pass/build-ast-to-ir"
	FILES
	"details/pass/build-ast-to-ir/context.cpp"
	"details/pass/build-ast-to-ir/context.h"
	"details/pass/build-ast-to-ir/context.hxx"
	"details/pass/build-ast-to-ir/producer.h"
	"details/pass/build-ast-to-ir/scope.h"
	"details/pass/build-ast-to-ir/scope.hxx"
	"details/pass/build-ast-to-ir/scope.cpp"
	"details/pass/build-ast-to-ir/scope-for.cpp"
	"details/pass/build-ast-to-ir/scope-in.cpp"
	"details/pass/build-ast-to-ir/scope-if.cpp"
	"details/pass/build-ast-to-ir/scope-while.cpp"
	"details/pass/build-ast-to-ir/scope-switch.cpp"
	"details/pass/build-ast-to-ir/scope-func.cpp"
	"details/pass/build-ast-to-ir/scope-closure.cpp"
	"details/pass/build-ast-to-ir/scope-typeof.cpp"
	"details/pass/build-ast-to-ir/scope-typedef.cpp"
	"details/pass/build-ast-to-ir/scope-new.cpp"
	"details/pass/build-ast-to-ir/scope-var.cpp"
	"details/pass/build-ast-to-ir/scope-class.cpp"
	"details/pass/build-ast-to-ir/scope-expr.cpp"
	"details/pass/build-ast-to-ir/scope-call.cpp"
	"details/pass/build-ast-to-ir/scope-return.cpp"
	"details/pass/build-ast-to-ir/scope-stmt.cpp"
	"details/pass/build-ast-to-ir/scope-scope.cpp"
	"details/pass/build-ast-to-ir/scope-type.cpp"
	"details/pass/build-ast-to-ir/scope-number.cpp"
	"details/pass/build-ast-to-ir/scope-string.cpp"
	"details/pass/build-ast-to-ir/scope-intrinsic.cpp"
	"details/pass/build-ast-to-ir/scope-generic-type-parameters.cpp"
	"details/pass/build-ast-to-ir/source-ast-to-ir.cpp")


addsrc(
	NAME  nysrc_details_pass_build_attach
	GROUP "details/pass/build-attach"
	FILES
	"details/pass/build-attach-program/mapping.cpp"
	"details/pass/build-attach-program/mapping.h"
	"details/pass/build-attach-program/attach.cpp")


addsrc(
	NAME  nysrc_details_pass_build_instanciate
	GROUP "details/pass/build-instanciate"
	FILES
	"details/pass/build-instanciate/instanciate.cpp"
	"details/pass/build-instanciate/instanciate-atom.cpp"
	"details/pass/build-instanciate/instanciate-atom.h"
	"details/pass/build-instanciate/instanciate.h"
	"details/pass/build-instanciate/instanciate.hxx"
	"details/pass/build-instanciate/stack-frame.h"
	"details/pass/build-instanciate/stack-frame.hxx"
	"details/pass/build-instanciate/instanciate-capture-variable.cpp"
	"details/pass/build-instanciate/instanciate-identify.cpp"
	"details/pass/build-instanciate/instanciate-ensure-resolved.cpp"
	"details/pass/build-instanciate/instanciate-self.cpp"
	"details/pass/build-instanciate/instanciate-allocate.cpp"
	"details/pass/build-instanciate/instanciate-stacksize.cpp"
	"details/pass/build-instanciate/instanciate-alias.cpp"
	"details/pass/build-instanciate/instanciate-store.cpp"
	"details/pass/build-instanciate/instanciate-push.cpp"
	"details/pass/build-instanciate/instanciate-builtin.cpp"
	"details/pass/build-instanciate/instanciate-call.cpp"
	"details/pass/build-instanciate/instanciate-sizeof.cpp"
	"details/pass/build-instanciate/instanciate-error.cpp"
	"details/pass/build-instanciate/instanciate-intrinsic.cpp"
	"details/pass/build-instanciate/instanciate-intrinsic-builtin.cpp"
	"details/pass/build-instanciate/instanciate-return.cpp"
	"details/pass/build-instanciate/instanciate-scope.cpp"
	"details/pass/build-instanciate/instanciate-pragma.cpp"
	"details/pass/build-instanciate/instanciate-blueprint.cpp"
	"details/pass/build-instanciate/instanciate-assignment.cpp"
	"details/pass/build-instanciate/instanciate-ref-unref.cpp"
	"details/pass/build-instanciate/instanciate-assign.cpp"
	"details/pass/build-instanciate/instanciate-typeisobject.cpp"
	"details/pass/build-instanciate/instanciate-member-variable-default-init.cpp"
	"details/pass/build-instanciate/instanciate-member-variable-default-dispose.cpp"
	"details/pass/build-instanciate/instanciate-member-variable-default-clone.cpp"
	"details/pass/build-instanciate/overloaded-func-call-resolution.cpp"
	"details/pass/build-instanciate/overloaded-func-call-resolution.h"
	"details/pass/build-instanciate/suggestion-not-declared-in-scope.cpp")


addsrc(
	NAME  nysrc_details_vm
	GROUP "details/vm"
	FILES
	"details/vm/vm.cpp"
	"details/vm/memchecker.h"
	"details/vm/memchecker.cpp"
	"details/vm/stacktrace.h"
	"details/vm/stacktrace.cpp"
	"details/vm/stack.cpp"
	"details/vm/stack.h"
	"details/vm/stack.hxx"
	"details/vm/types.h"
	"details/vm/vm.hxx"
	"details/vm/vm.h")


addsrc(
	NAME  nysrc_details_utils
	GROUP "details/utils"
	FILES
	"details/utils/origin.h"
	"details/utils/stringrefs.h"
	"details/utils/stringrefs.hxx"
	"details/utils/stringrefs.cpp"
	"details/utils/memory-allocator.h"
	"details/utils/check-for-valid-identifier-name.cpp"
	"details/utils/check-for-valid-identifier-name.h"
	"details/utils/clid.h"
	"details/utils/clid.hxx")


addsrc(
	NAME  nysrc_details_ast
	GROUP "details/ast"
	FILES
	"details/ast/tree-index.h"
	"details/ast/tree-index.hxx"
	"details/ast/tree-index.cpp"
	"details/ast/ast.h"
	"details/ast/ast.hxx"
	"details/ast/ast.cpp")


addsrc(
	NAME  nysrc_details_ir
	GROUP "details/ir"
	FILES
	"details/ir/ir.h"
	"details/ir/isa/opcodes.h"
	"details/ir/isa/pragma.h"
	"details/ir/isa/opcodes.cpp"
	"details/ir/isa/data.h"
	"details/ir/isa/data.cpp"
	"details/ir/isa/printer.inc.hpp"
	"details/ir/scope-locker.h"
	"details/ir/instruction.h"
	"details/ir/sequence.h"
	"details/ir/sequence.hxx"
	"details/ir/sequence.cpp")


addsrc(
	NAME  nysrc_details_atom_classdef
	GROUP "details/atom/classdef"
	FILES
	"details/atom/classdef.h"
	"details/atom/classdef.hxx"
	"details/atom/classdef.cpp"
	"details/atom/classdef-table.h"
	"details/atom/classdef-table.hxx"
	"details/atom/classdef-table.cpp"
	"details/atom/classdef-table-view.h"
	"details/atom/classdef-table-view.hxx"
	"details/atom/classdef-table-view.cpp"
	"details/atom/classdef-follow.h"
	"details/atom/classdef-follow.hxx"
	"details/atom/classdef-follow.cpp"
	"details/atom/classdef-overloads.h"
	"details/atom/classdef-overloads.hxx"
	"details/atom/classdef-overloads.cpp")


addsrc(
	NAME nysrc_details_type_system
	GROUP "details/type"
	FILES
	"details/type/builtin.h"
	"details/type/type-check.cpp"
	"details/type/type-check.h")


addsrc(
	NAME  nysrc_details_atom
	GROUP "details/atom"
	FILES
	"details/atom/funcdef.h"
	"details/atom/funcdef.cpp"
	"details/atom/funcdef.hxx"
	"details/atom/type.h"
	"details/atom/qualifiers.h"
	"details/atom/qualifiers.hxx"
	"details/atom/interface.h"
	"details/atom/interface.cpp"
	"details/atom/interface.hxx"
	"details/atom/vardef.h"
	"details/atom/vardef.hxx"
	"details/atom/vardef.cpp"
	"details/atom/local-variables.h"
	"details/atom/local-variables.hxx"
	"details/atom/local-variables.cpp"
	"details/atom/atom.h"
	"details/atom/atom.hxx"
	"details/atom/atom.cpp"
	"details/atom/atom-map.h"
	"details/atom/atom-map.hxx"
	"details/atom/atom-map.cpp"
	"details/atom/signature.h"
	"details/atom/signature.cpp"
	"details/atom/signature.hxx"
	"details/atom/func-overload-match.cpp"
	"details/atom/func-overload-match.h")


addsrc(
	NAME  nysrc_details_intrinsic
	GROUP "details/intrinsic"
	FILES
	"details/intrinsic/intrinsic.h"
	"details/intrinsic/intrinsic.cpp"
	"details/intrinsic/intrinsic-table.h"
	"details/intrinsic/intrinsic-table.hxx"
	"details/intrinsic/intrinsic-table.cpp")


addsrc(
	NAME  nysrc_details_context
	GROUP "details/context"
	FILES
	"details/context/project.h"
	"details/context/project.hxx"
	"details/context/project.cpp"
	"details/context/build.h"
	"details/context/build.cpp"

	"details/context/build-info.h"
	"details/context/build-info.cpp"
	"details/context/target.h"
	"details/context/target.hxx"
	"details/context/target.cpp"
	"details/context/source.h"
	"details/context/source.hxx"
	"details/context/source.cpp")


addsrc(
	NAME  nysrc_details_reporting
	GROUP "details/reporting"
	FILES
	"details/reporting/fwd.h"
	"details/reporting/message.h"
	"details/reporting/message.cpp"
	"details/reporting/report.cpp"
	"details/reporting/report.h"
	"details/reporting/report.hxx"
	"details/reporting/levels.h")


addsrc(
	NAME  nysrc_c_api
	GROUP "c-api"
	FILES
	"c-api/project.cpp"
	"c-api/build.cpp"
	"c-api/program.cpp"
	"c-api/console.cpp"
	"c-api/bugreport.cpp"
	"c-api/validator.cpp"
	"c-api/type.cpp"
	"c-api/print-ast.cpp"
	"c-api/visibility.cpp"
	"c-api/memalloc.c"
	"c-api/common-debuginfo.hxx"
	"c-api/version.c")





set(mode_libnany "SHARED")
if ("${NANY_STATIC}")
	set(mode_libnany "STATIC")
endif()


add_library(libnany-grammar STATIC EXCLUDE_FROM_ALL
	${nysrc_grammar}
	${nysrc_details_grammar})

add_dependencies(libnany-grammar nany-grammar-cpp)


add_library(libnany ${mode_libnany}
	# public headers
	nany/nany.h
	nany/types.h
	nany/nany.hxx

	${nysrc_c_api}

	${nysrc_details_pass_build_ast_from_source}
	${nysrc_details_pass_build_ast_normalize}
	${nysrc_details_pass_build_ast_to_ir}
	${nysrc_details_pass_build_attach}
	${nysrc_details_pass_build_instanciate}

	${nysrc_details_vm}

	${nysrc_details_utils}
	${nysrc_details_ast}
	${nysrc_details_ir}
	${nysrc_details_atom_classdef}
	${nysrc_details_atom}
	${nysrc_details_type_system}
	${nysrc_details_intrinsic}
	${nysrc_details_context}
	${nysrc_details_reporting}

	# Semantic Analysis
	details/sema/metadata.cpp
	details/sema/metadata.h
	# implementation - misc
	details/fwd.h

	# NSL - std.core
	details/nsl/import-stdcore.h
	details/nsl/import-stdcore-string.cpp
	details/nsl/import-stdos-process.cpp
	details/nsl/import-stdio-native.cpp
	details/nsl/import-stdenv.cpp
	${nysrc_nsl_stdcore}

	# misc
	"${libnany_config_folder}/libnany-config.h"
	"${libnany_config_folder}/libnany-traces.h"
	"${libnany_config_folder}/libnany-version.h")



set_target_properties(libnany PROPERTIES
	VERSION   "${nany_version_major}.${nany_version_minor}.${nany_version_patch}"
	SOVERSION "${nany_version_major}")

set_target_properties(libnany-grammar PROPERTIES
	VERSION   "${nany_version_major}.${nany_version_minor}.${nany_version_patch}"
	SOVERSION "${nany_version_major}")

target_link_libraries(libnany PRIVATE
	yuni-static-core libnany-grammar dyncall_s)

set_property(TARGET libnany PROPERTY MACOSX_RPATH true)
set_property(TARGET libnany PROPERTY LIBRARY_OUTPUT_NAME "nany")
set_property(TARGET libnany PROPERTY ARCHIVE_OUTPUT_NAME "nany")
set_property(TARGET libnany PROPERTY LIBRARY_OUTPUT_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}/../")
set_property(TARGET libnany PROPERTY RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}/../")

set_property(TARGET libnany-grammar PROPERTY MACOSX_RPATH true)
set_property(TARGET libnany-grammar PROPERTY LIBRARY_OUTPUT_NAME "nany-grammar")
set_property(TARGET libnany-grammar PROPERTY ARCHIVE_OUTPUT_NAME "nany-grammar")


target_compile_definitions(libnany PRIVATE "LIBNANY_DLL_EXPORT=1")


target_include_directories(libnany PRIVATE "${libnany_config_folder}")
target_include_directories(libnany PRIVATE "${CMAKE_CURRENT_LIST_DIR}/../ext/dyncall/")
target_include_directories(libnany PUBLIC  "${CMAKE_CURRENT_LIST_DIR}")
target_include_directories(libnany-grammar PUBLIC "${CMAKE_CURRENT_LIST_DIR}")

add_dependencies(libnany nany-grammar-cpp)
add_dependencies(libnany nyt-nanyfile-to-cstring)


install(TARGETS libnany
	LIBRARY DESTINATION lib
	ARCHIVE DESTINATION lib
	COMPONENT "libnany")



# The end !
